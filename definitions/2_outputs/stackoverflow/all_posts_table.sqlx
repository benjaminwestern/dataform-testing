config {
  type: "table", // Defined this as a table to show the discrepancy between view and table for dependencies
  schema: "outputs",
  name: "all_posts_table",
  tags: ["daily"],
  description: "Combine both questions and answers into a single posts_all table)",
  dependOnDependencyAssertions: true,
  dependencies: [{
      schema: "intermediate",
      name: "answers"
    },
    {
      schema: "intermediate",
      name: "questions"
    },
    {
      // This is an assertion dependency listed in `3_assertions/posts_count.sqlx`
      name: "posts_count",
      // includeDependentAssertions: false -- adding this flag would make the dataform run fail
    }
  ],
  uniqueKey: ["post_id"],
  columns: {
    post_id: `A unique identifier for a post`,
    created_at: `The time the post was created`,
    type: `The post type: question or answer`,
    title: `The title of the post`,
    body: `The content in the body of the post`
  },
  assertions: {
    uniqueKey: ["post_id"],
    nonNull: ["post_id", "created_at"]
  }
}


SELECT
    post_id,
    created_at,
    type,
    title,
    body,
    owner_user_id,
    parent_id
FROM
    ${ref("questions")}
UNION ALL
SELECT
    post_id,
    created_at,
    type,
    title,
    body,
    owner_user_id,
    parent_id
FROM
    ${ref("answers")}
